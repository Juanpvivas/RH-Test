apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def secretsPropertiesFile = rootProject.file("config.properties")
def secretProperties = new Properties()
if (secretsPropertiesFile.exists()) {
    secretProperties.load(new FileInputStream(secretsPropertiesFile))
} else {
    secretProperties.setProperty("STORE_FILE", "${System.getenv('BITRISEIO_ANDROID_KEYSTORE_URL')}")
    secretProperties.setProperty("STORE_PASSWORD", "${System.getenv('STORE_PASSWORD')}")
    secretProperties.setProperty("KEY_ALIAS", "${System.getenv('KEY_ALIAS')}")
    secretProperties.setProperty("KEY_PASSWORD", "${System.getenv('KEY_PASSWORD')}")
}

android {
    signingConfigs {
        release {
            storeFile file("${secretProperties['STORE_FILE']}")
            storePassword "${secretProperties['STORE_PASSWORD']}"
            keyAlias "${secretProperties['KEY_ALIAS']}"
            keyPassword "${secretProperties['KEY_PASSWORD']}"
        }
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "com.vivcom.rhtest"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            //Insert keys
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation project(":data")
    implementation project(":domain")
    implementation project(":usecases")

    implementation androidLibs.values()
    implementation kotlinLibs.values()
    implementation libs.values()
    kapt androidKaptLibs.values()

    testImplementation testLibs.values()
    testImplementation androidxTestLibs.values()
    androidTestImplementation androidTestLibs.values()
}
